AC_INIT(configure.in)

LOG4CPP_MAJOR_VERSION=0
LOG4CPP_MINOR_VERSION=2
LOG4CPP_MICRO_VERSION=2

PACKAGE=log4cpp
VERSION=$LOG4CPP_MAJOR_VERSION.$LOG4CPP_MINOR_VERSION.$LOG4CPP_MICRO_VERSION
RELEASE=$LOG4CPP_MAJOR_VERSION.$LOG4CPP_MINOR_VERSION

#
# +1 : ? : +1  == new interface that does not break old one
# +1 : ? : 0   == new interface that breaks old one
#  ? : ? : 0   == no new interfaces, but breaks apps
#  ? :+1 : ?   == just some internal changes, nothing breaks but might work 
#                 better
# CURRENT : REVISION : AGE
LT_VERSION=0:2:0

AC_SUBST(LOG4CPP_MAJOR_VERSION)
AC_SUBST(LOG4CPP_MINOR_VERSION)
AC_SUBST(LT_VERSION)

AC_CONFIG_AUX_DIR(config)
AM_CONFIG_HEADER(include/log4cpp/config.h)
AM_INIT_AUTOMAKE($PACKAGE, $VERSION)

# General "with" options
# ----------------------------------------------------------------------------
AC_ARG_WITH(idsa, [  --with-idsa             include idsa support])
AC_ARG_ENABLE(doxygen, [  --enable-doxygen        enable documentation generation with doxygen (auto)])
AC_ARG_ENABLE(dot, [  --enable-dot            use 'dot' to generate graphs in doxygen (auto)])              
AC_ARG_ENABLE(html-docs, [  --enable-html-docs      enable HTML generation with doxygen (yes)], [], [ enable_html_docs=yes])              
AC_ARG_ENABLE(latex-docs, [  --enable-latex-docs     enable LaTeX documentation generation with doxygen (no)], [], [ enable_latex_docs=no])              

# Checks for programs
# ----------------------------------------------------------------------------
AC_CANONICAL_HOST

AM_PROG_LIBTOOL
AC_PROG_INSTALL
AC_PROG_MAKE_SET

AC_PROG_CXX
AC_PROG_CXXCPP
AC_LANG_CPLUSPLUS

# Checks for headers

AC_MSG_CHECKING([for std::stringstream])
AC_TRY_COMPILE([#include <sstream>], [std::stringstream s;], 
               HAVE_STRINGSTREAM=yes, HAVE_STRINGSTREAM=no)

if test "$HAVE_STRINGSTREAM" = "yes" ; then
        AC_DEFINE(LOG4CPP_USE_NEW_IOSTREAM, 1, 
                  [define if you have new iostreams])
        AC_MSG_RESULT(yes)
else
        AC_MSG_RESULT(no)
        AC_MSG_CHECKING([for vform() in ostrstream])
        AC_TRY_COMPILE([#include <strstream>
                        #include <stdarg.h>
                       ], 
                       [using namespace std; va_list args;
                        ostrstream s; s.vform("%s", args);
                       ], 
                       HAVE_STRSTREAM_VFORM=yes, HAVE_STRSTREAM_VFORM=no)
        if test "$HAVE_STRSTREAM_VFORM" = "yes" ; then
                AC_DEFINE(HAVE_STRSTREAM_VFORM, 1, 
                          [define if ostrstream has vform()])
                AC_MSG_RESULT(yes)
        else
                AC_MSG_RESULT(no)
        fi
fi


# Checks for libraries
# ----------------------------------------------------------------------------

# Checks local idioms
# ----------------------------------------------------------------------------

# check for extensions
# ----------------------------------------------------------------------------
if test "x$enable_doxygen" = xno; then
        enable_doc=no
else 
        AC_PATH_PROG(DOXYGEN, doxygen, , $PATH)
        if test x$DOXYGEN = x; then
                if test "x$enable_doxygen" = xyes; then
                        AC_MSG_ERROR([could not find doxygen])
                fi
                enable_doc=no
        else
                enable_doc=yes
                AC_PATH_PROG(DOT, dot, , $PATH)
        fi
fi
AM_CONDITIONAL(DOC, test x$enable_doc = xyes)

if test x$DOT = x; then
        if test "x$enable_dot" = xyes; then
                AC_MSG_ERROR([could not find dot])
        fi
        enable_dot=no
else
        enable_dot=yes
fi
AC_SUBST(enable_dot)
AC_SUBST(enable_html_docs)
AC_SUBST(enable_latex_docs)

# CXX fine tuning
# ----------------------------------------------------------------------------
case "$host" in
    *-dec-osf*)	  
	CXXFLAGS="$CXXFLAGS -std strict_ansi_errors" 
	;;
    *)	
	;;
esac

if test "$GCC" = yes; then
    case `$CXX --version` in
	*2.97*) CXXFLAGS="$CXXFLAGS -Wall -D_ISOC99_SOURCE" ;;
	*)      CXXFLAGS="$CXXFLAGS -Wall" ;;
    esac
fi

# syslog_test
AC_CHECK_FUNCS([syslog])

# idsa_test

if test "x$with_idsa" = xyes; then
    AC_CHECK_LIB([idsa], [idsa_open])
    if test "$ac_cv_lib_idsa_idsa_open" = no; then
        AC_MSG_ERROR([could not locate idsa library])
    fi
fi

# Create files
# ----------------------------------------------------------------------------
AC_OUTPUT([
Makefile
log4cpp.spec
config/Makefile
doc/Makefile
doc/Doxyfile
src/Makefile
include/Makefile
include/log4cpp/Makefile
tests/Makefile
])
